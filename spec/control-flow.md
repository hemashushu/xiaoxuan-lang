# 流程控制

<!-- @import "[TOC]" {cmd="toc" depthFrom=1 depthTo=6 orderedList=false} -->

<!-- code_chunk_output -->

- [流程控制](#流程控制)
  - [条件语句](#条件语句)
    - [`如果` 语句](#如果-语句)
      - [多行格式](#多行格式)
      - [省略 `else` 语句](#省略-else-语句)
      - [多个条件语句组合](#多个条件语句组合)
    - [`条件` 语句](#条件-语句)
      - [条件语句的局部变量](#条件语句的局部变量)
    - [`选择` 语句](#选择-语句)
  - [循环语句](#循环语句)
    - [`遍历` 语句](#遍历-语句)
    - ["设有 让" 语句](#设有-让-语句)
    - [指定次数循环](#指定次数循环)

<!-- /code_chunk_output -->

## 条件语句

### `如果` 语句

`如果`（`if`） 条件语句的语法：

* `如果 ... 那么 ... 否则 ...`
* `if ... then ... else ...`

语句有 3 个表达式：

1. 第一个表达式是一个值为 `逻辑`（`Boolean`） 的条件表达式；
2. 如果条件表达式的值为 true，则返回第二个表达式的值；
3. 如果条件表达式的值为 false，则返回第三个表达式的值。

示例：

```js
让 x = 90
让 a = 如果 x >= 60 那么 "及格" 否则 "不及格"
```

需要注意：

* 条件表达式只接受逻辑类型的返回值；
* `那么` 和 `否则` 两个表达式的返回值的数据类型必须一致；
* 如果忽略条件语句的返回值（即条件语句的返回值不赋值给一个变量），则对 `那么` 和 `否则` 表达式返回值的数据类型不作要求。

#### 多行格式

`如果` 语句可以写成多行格式，可以在 `那么`、`否则` 后面换行，然后最后加上 `以上` 表示语句块结束。另外，在条件表达式后面也是可以换行的。下列都是合法的多行格式：

```js
如果 ...
那么 ...
否则 ...
```

```js
如果 ...
那么
    ...
否则
    ...
以上
```

```js
如果 ... 那么
    ...
否则
    ...
以上
```

```js
if ...
then ...
else ...
```

```js
if ...
then
    ...
else
    ...
end
```

```js
if ... then
    ...
else
    ...
end
```

#### 省略 `else` 语句

如果忽略条件语句的返回值，也可以省略 `else` 语句，示例：

```js
如果 x > 90 那么
    输出行 ("优秀")
以上
```

#### 多个条件语句组合

在 `否则` 后面，也可以追加另一个 `如果` 语句，比如：

```js
让 x = 89
如果 x >= 90 那么
    输出行 ("优秀")
否则 如果 x >= 80 那么
    输出行 ("好")
否则 如果 x >= 60 那么
    输出行 ("良")
否则
    输出行 ("加油")
以上
```

```js
let x = 89
if x >= 90 then
    writeLine ("Excellent")
else if x >= 80 then
    writeLine ("Good")
else if x >= 60 then
    writeLine ("Fair")
else
    writeLine ("Poor")
end
```

### `条件` 语句

当有多个条件时，除了可以使用多个 `如果` 语句组合，还能使用 `条件` 语句。

`条件` 语句的语法：

```js
条件
    情况 条件表达式1: 语句1
    情况 条件表达式2: 语句2
    ...
    默认: 语句N
以上
```

```js
condition
    case expr1: statement1
    case expr2: statement2
    ...
    default: statementN
end
```

其中最后一个 `默认` 语句是可选的。

跟 `如果` 语句一样，条件语句也能返回值。示例：

```js
让 x = 70
让 s = 条件
    情况 x>=90: "优"
    情况 x>=80: "好"
    情况 x>=60: "良"
    默认: "加油"
以上
```

```js
let x = 70
let s = condition
    case x>=90: "Excellent"
    case x>=80: "Good"
    case x>=60: "Fair"
    default: "Poor"
end
```

#### 条件语句的局部变量

有时需要在条件的 `情况`（`case`） 里使用某些计算后的值，但这些值对于条件语句之外没有用处，这时可以使用 `其中`（`where`）关键字在条件里定义及计算局部变量。示例：

```js
让 x = 70
让 s = 条件
    其中 isEven = x `余` 2 == 0,
         isOdd = x `余` 2 != 0
    情况 x >= 90 && isEven: "优.偶数"
    情况 x >= 90 && isOdd: "优.奇数"
    ...
以上
```

```js
let x = 70
let s = condition
    where isEven = x `rem` 2 == 0,
         isOdd = x `rem` 2 != 0
    case x >= 90 && isEven: "Excellent.Even"
    case x >= 90 && isOdd: "Excellent.Odd"
    ...
end
```

`其中` 关键字后面加上一个或多个变量的声明和赋值，多个变量之间使用逗号 "," 分隔。

`其中` 关键字也可以写在 `情况` 的后面，表示其声明的变量仅仅在当前情况下使用，比如上例可以写成：

```js
让 x = 70
让 s = 条件
    情况 x >= 90 && isEven
        其中 isEven = x `余` 2 == 0
        :"优.偶数"
    情况 x >= 90 && isOdd
        其中 isOdd = x `余` 2 != 0
        :"优.奇数"
    ...
以上
```

```js
let x = 70
let s = condition
    case x >= 90 && isEven
        where isEven = x `rem` 2 == 0
        : "Excellent.Even"
    case x >= 90 && isOdd
        where isOdd = x `rem` 2 != 0
        : "Excellent.Odd"
    ...
end
```

### `选择` 语句

如果需要一个值与多个候选值比较，可以使用 `选择`（`switch`）语句。

`选择` 语句的语法：

```js
选择 variable
    情况 value1: ...
    情况 value2: ...
    情况 value3: ...
    默认: ...
以上
```

```js
switch variable
    case value1: ...
    case value2: ...
    case value3: ...
    default: ...
end
```

选择语句跟条件语句不同的地方在于，选择语句仅仅让指定的变量的值跟每个情况里的字面量或者常量进行相等比较。

`情况` 后面可以跟一个或多个字面量或者常量，只需用逗号分隔即可。

示例：

```js
让 d = 1
选择 d
    情况 1,2,3,4,5:
        输出行("工作日")
    情况 6,7:
        输出行("假日")
以上
```

```js
let d = 1
switch d
    case 1,2,3,4,5:
        writeLine("Working days")
    case 6,7:
        writeLine("Holiday")
end
```

跟 `如果`，`条件` 语句一样，`选择` 语句也是可以返回值的，示例：

```js
让 d = 2
让 s = 选择 d
    情况 1,2,3,4,5: "工作日"
    情况 6,7: "假日"
以上
输出行(s)
```

```js
let d = 2
let s = switch d
    case 1,2,3,4,5: "Working days"
    case 6,7: "Holiday"
end
writeLine(s)
```

跟其他编程语言的 `switch` 语句不一样，XiaoXuan 的 `情况` 后面不需要 `break` 关键字。

## 循环语句

### `遍历` 语句

`遍历`（`iterate`）语句用于遍历一个列表或者实现了 `序列` 类别的数据到一个指定的变量，相当于 Java 语言的 "for...each" 语句。

示例：

```js
遍历 [1..10] 到 i
    输出行 (i)
以上
```

```js
iterate [1..10] to i
    writeLine (i)
end
```

`遍历` 语句的主体实质上是一个递归的匿名函数，其参数名为 `到` 关键字后面的变量，遍历语句使用 `[x,...xs]` 模式从一个类列表数据获取第一个元素及余下的元素，然后让第一个元素作为实参调用匿名函数，重复这个过程直到类列表的元素都被读取一遍。

上面的示例大致相当于：

```js
function iter (Int[] [])
    // empty
end

function iter (Int[] [i, ...remains])
    writeLine (i)
    iter (remains)
end

iter ([1..10])
```

或者

```js
function iter (Int[] list, f) condition
    case []:
        return
    case [i, ...remains]:
        f (i)
        iter (remains)
end

iter ([1..10], fn (i) writeLine (i))
```

`遍历` 语句无返回值（或者说返回的是 `Result<Unit>`），如果想遍历一个列表且根据一定的关系返回值，可以使用 `映射`（`map`） 或者 `折叠`（`fold`） 函数。

### "设有 让" 语句

`设有 让`（`for let`）是一种类似 Java 的 "for" 语句的循环语句， "for" 语句由一个变量及初始值、一个条件表达式、让变量变化的语句以及循环主体共 4 个部分组成。`设有 让` 语句的组成与 "for" 语句类似。

示例：

```js
设有 让 i = 0 如果 i < 100 那么
    ...
    输出行 (i)
    ...
    重复 i+1
以上
```

```js
for let i = 0 if i < 100 then
    ...
    writeLine(i)
    ...
    loop i+1
end
```

上面语句的作用等同于：

```JavaScript
for(let i=0; i<100; i=i+1) {
    console.log(i)
}
```

`设有 让` 语句当中：

* `让 i = 0` 是循环的变量名称及初始值，`设有 让` 语句只能设置一个变量（当然变量可以是任何数据类型，如果使用元组，则变相等同于支持多个变量）
* `如果 i < 100 那么 ... 以上` 是循环的条件语句（同时也是循环的主体），如果条件表达式返回 `真` 值，则执行循环主体，否则结束循环。
* `重复 i+1` 用于赋予变量新值，以及再次执行条件语句（即循环的主体）。

`设有 让` 语句实质上是一个带有一个（且只有一个）参数的匿名函数。

上面的示例将第一行换行之后得：

```js
设有 让 i = 0
    如果 i < 100 那么
        ...
        输出行 (i)
        ...
        重复 i+1
    以上
以上
```

```js
for let i = a
    if i < 100 then
        ...
        writeLine(i)
        ...
        loop i+1
    end
end
```

现在可以比较明显地看出，语句的第一行的作用是声明一个匿名函数，该匿名函数只有一个参数，且被赋予初始值。接下来是一个普通的 `如果` 条件语句，而 `重复` 关键字只是调用当前匿名函数的语法糖。上面的示例的等同代码如下：

```js
01  让 f = 匿名函数 (整数 i)
02      如果 i < 100 那么
03          ...
04          输出行 (i)
05          ...
06          f (i+1)
07      以上
08  以上
09
10  f (0)
```

其中 06 行是调用函数自己（相当于 `重复` 关键字），10 行是初始值及开始执行。

最基本的 `设有 让` 语句只需：

1. "设有 让" 关键字；
2. 定义变量及赋予初始值；
3. "重复" 关键字。

也就是说，条件语句其实不是 `设有 让` 语句的必要部分，比如下面是最精简的 `设有 让` 循环语句：

```js
设有 让 i = 0
    ...
    循环 i+1
    ...
以上
```

需注意：

* 要在设定在一定的条件下停止调用 `重复` 关键字，否则会陷入死循环。
* 如果漏写了 `重复` 关键字语句，循环体仍然会被执行一次（如果条件表达式的值为 `真` 的话）；
* `重复`（`loop`）是关键字，不是函数名称，后面直接跟着变量的新值，不需要加括号。如果变量是元组，则需要加上括号（此时括号表示元组）。
* `设有 让` 循环语句可以返回值。
* `设有 让` 循环语句不支持 `continue` 和 `break` 等关键字。只要不调用 `重复` 则相当于 `break`，至于 `continue` 语句可以使用一个大范围的 `如果` 条件语句代替。

下面演示如果通过元组实现在循环体里使用多个变量：

```js
for let (x,y) = (0, 0) if x > 10 && y < 100
    ...
    writeLine (y)
    ...
    loop (x+1, y+1)
end
```

### 指定次数循环

因为循环指定次数比较常见，所以还有一种重复指定次数的循环语句 `重复次数` 语句。

示例：

```js
重复次数 10
    输出行 ("你好")
以上
```

```js
repeat 10
    writeLine ("Hello")
end
```

`重复次数` 关键字后面加一个自然数，表示循环的次数。在循环主体里可以使用特殊变量 `%` 获取当前循环的次数。

`重复次数` 语句实际上是 `遍历` 语句的语法糖，上面的语句相当于：

```js
遍历 [1..10] 到 %
    输出行 ("你好")
以上
```

`重复次数` 语句无返回值。
